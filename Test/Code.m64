struct Bitmap
{
	memory: @uint;
	width: int;
	height: int;
}

func FillWithColor(bitmap: @Bitmap, color: uint)
{
	pixel := bitmap.memory;
	for row := 0; row < bitmap.height; row++
	{
		for col := 0; col < bitmap.width; col++
		{
			pixel@ = color;
			pixel++;
		}
	}
}

func SetPixelColor(bitmap: @Bitmap, row, col: int, color: uint)
{
	bitmap.memory[row * bitmap.width + col] = color;
}

struct float2
{
	x, y: float;
}

struct Quad2
{
	use p:[4]float2;
}

func float2_xy(x, y: float) float2
{
	result: float2;
	result.x = x;
	result.y = y;
	return result;
}

func TurnToRight(v : float2) float2
{
	return float2_xy(-v.y, v.x);
}

operator* mul_float_float2(x: float, v: float2) float2
{
	return float2_xy(x * v.x, x * v.y);
}

operator- sub_float2(p1:float2, p2:float2) float2
{
	return float2_xy(p1.x - p2.x, p1.y - p2.y);
}

operator+ add_float2(p1:float2, p2:float2) float2
{
	return float2_xy(p1.x + p2.x, p1.y + p2.y);
}

func GetRotatedQuadAroundPoint(center: float2, cos_sin: float2, size: float2) Quad2
{
	q:Quad2;
	
	y_dir := cos_sin;
	x_dir := TurnToRight(cos_sin);
	
	to_y := (0.5 * size.y) * y_dir;
	to_x := (0.5 * size.x) * x_dir;
	
	top := center + to_y;
	bottom := center - to_y;
	
	q[0] = top + to_x;
	q[1] = top - to_x;
	q[2] = bottom - to_x;
	q[3] = bottom + to_x;
	
	return q;
}

func TurnsRight(p0, p1, p2: float2) bool
{
	d0 := p1 - p0;
	d1 := p2 - p0;
	
	det := (d0.x * d1.y) - (d0.y * d1.x);
	turns_right := (det < 0.0);
	return turns_right;
}

func IsPointInQuad2(p: float2, q: Quad2) bool
{
	is_inside := true;
	is_inside &= TurnsRight(q[0], q[1], p);
	is_inside &= TurnsRight(q[1], q[2], p);
	is_inside &= TurnsRight(q[2], q[3], p);
	is_inside &= TurnsRight(q[3], q[0], p);
	return is_inside;
}

func DrawQuad2(bitmap: @Bitmap, quad: Quad2, color: uint)
{
	min_x := quad[0].x;
	max_x := quad[0].x;
	min_y := quad[0].y;
	max_y := quad[0].y;
	
	for i := 1; i < 4; i++
	{
		x := quad[i].x;
		y := quad[i].y;
		if x < min_x
		{
			min_x = x;
		}
		if x > max_x
		{
			max_x = x;
		}
		if y < min_y
		{
			min_y = y;
		}
		if y > max_y
		{
			max_y = y;
		}
	}
	
	for row := int::min_y; row < int::max_y + 1; row++
	{
		for col := int::min_x; col < int::max_x + 1; col++
		{
			p := float2_xy(float::col, float::row);
			if IsPointInQuad2(p, quad)
			{
				SetPixelColor(bitmap, row, col, color);
			}
		}
	}
}

func DrawRectMinMax(bitmap: @Bitmap, min_row, min_col, max_row, max_col: int, color: uint)
{
	for row := min_row; row <= max_row; row++
	{
		for col := min_col; col <= max_col; col++
		{
			index := row * bitmap.width + col;
			bitmap.memory[index] = color;
		}
	}
}

struct Input
{
	screen_size: float2;
	time: float;
}

func Min2(x, y : float) float
{
	if x < y
	{
		return x;
	}
	return y;
}

extern func cosf(x: float) float;
extern func sinf(x: float) float;

struct float3
{
	x, y, z: float;
}

func float3_xyz(x, y, z: float) float3
{
	r: float3;
	r.x = x;
	r.y = y;
	r.z = z;
	return r;
}

operator+ add_float3(p1: float3, p2: float3) float3
{
	return float3_xyz(p1.x + p2.x, p1.y + p2.y, p1.z + p2.z);
}

operator- sub_float3(p1: float3, p2: float3) float3
{
	return float3_xyz(p1.x - p2.x, p1.y - p2.y, p1.z - p2.z);
}

struct float3x3
{
	v: [3][3]float;
}

func float3x3_v(v00, v01, v02, v10, v11, v12, v20, v21, v22: float) float3x3
{
	m: float3x3;
	m.v[0][0] = v00;
	m.v[0][1] = v01;
	m.v[0][2] = v02;
	m.v[1][0] = v10;
	m.v[1][1] = v11;
	m.v[1][2] = v12;
	m.v[2][0] = v20;
	m.v[2][1] = v21;
	m.v[2][2] = v22;
	return m;
}

operator* transform3(m: float3x3, v: float3) float3
{
	result: float3;
	result.x = m.v[0][0] * v.x + m.v[0][1] * v.y + m.v[0][2] * v.z;
	result.y = m.v[1][0] * v.x + m.v[1][1] * v.y + m.v[1][2] * v.z;
	result.z = m.v[2][0] * v.x + m.v[2][1] * v.y + m.v[2][2] * v.z;
	return result;
}

func float3_xy_z(xy: float2, z: float) float3
{
	r: float3;
	r.x = xy.x;
	r.y = xy.y;
	r.z = z;
	return r;
}

func GetRotationAroundY(cos_sin: float2) float3x3
{
	c := cos_sin.x;
	s := cos_sin.y;
	
	tm := float3x3_v
	(
		  c, 0.0,   s,
		0.0, 1.0, 0.0,
		 -s, 0.0, c
	);

	return tm;
}

func ToXY(v: float3) float2
{
	return float2_xy(v.x, v.y);
}

func DrawQuad3(bitmap: @Bitmap, v1, v2, v3, v4: float3, color: uint)
{
	quad: Quad2;
	quad[0] = ToXY(v1);
	quad[1] = ToXY(v2);
	quad[2] = ToXY(v3);
	quad[3] = ToXY(v4);
	DrawQuad2(bitmap, quad, color);
}

func Update3D(input: @Input, bitmap: @Bitmap)
{
	FillWithColor(bitmap, uint::0);
	
	min_side := 0.5 * Min2(input.screen_size.x, input.screen_size.y);
	
	mid := 0.5 * input.screen_size;

	cube_center := float3_xy_z(mid, 0.0);
	cube_side := min_side;
	
	cos_sin := float2_xy(cosf(input.time), sinf(input.time));
	rot_tm := GetRotationAroundY(cos_sin);
	
	x_side := rot_tm * float3_xyz(0.5 * min_side, 0.0, 0.0);
	y_side := rot_tm * float3_xyz(0.0, 0.5 * min_side, 0.0);
	z_side := rot_tm * float3_xyz(0.0, 0.0, 0.5 * min_side);
	
	corner_ldf := cube_center - x_side - y_side - z_side;
	corner_ldb := cube_center - x_side - y_side + z_side;
	corner_luf := cube_center - x_side + y_side - z_side;
	corner_lub := cube_center - x_side + y_side + z_side;
	corner_rdf := cube_center + x_side - y_side - z_side;
	corner_rdb := cube_center + x_side - y_side + z_side;
	corner_ruf := cube_center + x_side + y_side - z_side;
	corner_rub := cube_center + x_side + y_side + z_side;
	
	DrawQuad3(bitmap, corner_luf, corner_ruf, corner_rdf, corner_ldf, uint::0x00FF00);
	DrawQuad3(bitmap, corner_ruf, corner_rub, corner_rdb, corner_rdf, uint::0xFF8800);
	DrawQuad3(bitmap, corner_rub, corner_lub, corner_ldb, corner_rdb, uint::0x0000FF);
	DrawQuad3(bitmap, corner_lub, corner_luf, corner_ldf, corner_ldb, uint::0xFF0000);
}

func Update(input: @Input, bitmap: @Bitmap)
{
	Update3D(input, bitmap);
}